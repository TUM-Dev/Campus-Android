plugins {
    id 'de.aaschmid.cpd' version '3.1'
}
// apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
//apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "com.github.ben-manes.versions"
apply plugin: 'com.google.firebase.crashlytics'
apply from: '../gradle/scripts/pmd.gradle'
apply from: '../gradle/scripts/jacoco.gradle'
apply from: '../gradle/scripts/checkstyle.gradle'
// apply from: '../gradle/scripts/detekt.gradle'
apply from: '../gradle/scripts/testLogging.gradle'

android {
    compileSdkVersion 31
    buildTypes {
        all {
            proguardFiles(file('../proguard').listFiles())
            proguardFile getDefaultProguardFile('proguard-android.txt')
            minifyEnabled true
        }
        release {
            debuggable false
            shrinkResources true
        }
        debug {
            debuggable true
            shrinkResources false
            testCoverageEnabled true
        }
    }
    defaultConfig {
        applicationId "de.tum.in.tumcampus"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 580
        versionName "3.14-dev"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        kapt.arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    lintOptions {
        checkAllWarnings true
        htmlReport false
        abortOnError false
        lintConfig new File("gradle/config/lint.xml")
        disable 'GoogleAppIndexingWarning',
                'TrulyRandom',
                'ContentDescription',
                'UnusedIds',
                'UnusedResources',    // unused resources get purged by shrinkResources
                'SelectableText'      // disabled by default
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            setIgnoreFailures(true)
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    androidExtensions {
        experimental = true
    }
}

def retrofitVersion = '2.9.0'
def tikXmlVersion = '0.8.13'

configurations.all {
    resolutionStrategy {
        force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

dependencies {

    // Support Libraries
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    // Android arch components
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'
    implementation 'android.arch.work:work-runtime-ktx:1.0.1'
    // Note: Appcompat 1.4 breaks sandwich menu injected into toolbar by DrawerLayout
    // upgrading this and or androidx.fragment:fragment-ktx will break the toolbar
    implementation 'androidx.appcompat:appcompat:1.2.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.4.0'
    kapt "androidx.room:room-compiler:$room_version"

    // Play services
    implementation 'com.google.android.gms:play-services-base:17.6.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // Used for in-app reviews
    implementation "com.google.android.play:core:$play_core_services"
    implementation "com.google.android.play:core-ktx:$play_core_services_kotlin_extension"

    // Firebase
    implementation 'com.google.firebase:firebase-core:20.0.0'
    implementation 'com.google.firebase:firebase-messaging:23.0.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.4'
    implementation 'com.google.firebase:firebase-analytics-ktx:20.0.0'

    // Helpers
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'net.danlew:android.joda:2.10.6.1'

    // XML Parsing
    implementation "com.tickaroo.tikxml:annotation:$tikXmlVersion"
    implementation "com.tickaroo.tikxml:core:$tikXmlVersion"
    kapt "com.tickaroo.tikxml:processor:$tikXmlVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:0.10.8"
    implementation 'androidx.core:core-ktx:1.7.0'
    // Note: upgrading this and or androidx.appcompat:appcompat will break the toolbar
    implementation 'androidx.fragment:fragment-ktx:1.2.5'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.trello.rxlifecycle3:rxlifecycle-android-lifecycle:3.1.0'
    implementation 'com.trello.rxlifecycle3:rxlifecycle:3.1.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation 'com.jakewharton.rxbinding3:rxbinding-material:3.1.0'
    implementation 'com.patloew.rxlocation:rxlocation:1.0.5'

    // Transport
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation "com.tickaroo.tikxml:retrofit-converter:$tikXmlVersion"
    implementation 'com.github.franmontiel:PersistentCookieJar:1.0.1'
    implementation 'com.squareup.okio:okio:2.8.0'

    // UI
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    implementation 'com.github.thellmund.Android-Week-View:core:4.1.6'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.28.3'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    // DebugDatabase
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // Testing
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.5.11'
    testImplementation 'org.assertj:assertj-core:3.17.1'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    testImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation "org.robolectric:robolectric:4.5.1"

    // TicketSale
    implementation 'com.stripe:stripe-android:6.1.2'

    // gRPC backend
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'io.grpc:grpc-okhttp:1.25.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-protobuf-lite:1.25.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.25.0' // CURRENT_GRPC_VERSION
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    //protobuf 'com.google.protobuf:protobuf-java:3.10.0'

    // View binding helper
    implementation "com.github.Zhuinden:fragmentviewbindingdelegate-kt:1.0.0"
}

/*protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.10.0' }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}*/

apply plugin: 'com.google.gms.google-services'
