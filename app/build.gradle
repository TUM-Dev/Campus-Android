plugins {
    id 'io.gitlab.arturbosch.detekt' version "1.0.0.RC6-4"
    id 'de.aaschmid.cpd' version '1.0'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply from: '../gradle/scripts/findbugs.gradle'
apply from: '../gradle/scripts/pmd.gradle'
apply from: '../gradle/scripts/jacoco.gradle'
apply from: '../gradle/scripts/checkstyle.gradle'
apply from: '../gradle/scripts/detekt.gradle'
apply from: '../gradle/scripts/testLogging.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    buildTypes {
        all {
            proguardFiles(file('../proguard').listFiles())
            proguardFile getDefaultProguardFile('proguard-android.txt')
            minifyEnabled true
            useProguard true
        }
        release {
            debuggable false
            shrinkResources true
        }
        debug {
            debuggable true
            shrinkResources false
            testCoverageEnabled true
        }
    }
    defaultConfig {
        applicationId "de.tum.in.tumcampus"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 562
        versionName "3.1-dev"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        kapt.arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    lintOptions {
        checkAllWarnings true
        htmlReport false
        abortOnError false
        lintConfig new File("gradle/config/lint.xml")
        disable 'GoogleAppIndexingWarning',
                'TrulyRandom',
                'ContentDescription',
                'UnusedIds',
                'UnusedResources',    // unused resources get purged by shrinkResources
                'SelectableText'      // disabled by default
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            setIgnoreFailures(true)
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

def retrofitVersion = '2.4.0'
def tikXmlVersion = '0.8.13'

configurations.all {
    resolutionStrategy {
        force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

dependencies {

    // Support Libraries
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'

    // Android arch components
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha04'
    implementation 'androidx.room:room-runtime:2.1.0-alpha04'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    kapt 'androidx.room:room-compiler:2.1.0-alpha04'

    // Play services
    implementation 'com.google.android.gms:play-services-base:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    // Helpers
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'net.danlew:android.joda:2.10.1'

    // XML Parsing
    implementation "com.tickaroo.tikxml:annotation:$tikXmlVersion"
    implementation "com.tickaroo.tikxml:core:$tikXmlVersion"
    kapt "com.tickaroo.tikxml:processor:$tikXmlVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:0.10.7"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android-lifecycle:2.2.2'
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.2'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    // Transport
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.tickaroo.tikxml:retrofit-converter:$tikXmlVersion"
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // UI
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'
    implementation 'com.github.chrisbanes:PhotoView:2.2.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.9.0'
    implementation 'com.github.thellmund:Android-Week-View:3.1.3'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    // Dagger
    implementation 'com.google.dagger:dagger:2.19'
    kapt 'com.google.dagger:dagger-compiler:2.19'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'androidx.room:room-testing:2.1.0-alpha04'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    testImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation "org.robolectric:robolectric:4.0.1"

    // TicketSale
    implementation 'com.stripe:stripe-android:6.1.2'
}

apply plugin: 'com.google.gms.google-services'
