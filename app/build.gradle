apply plugin: 'com.android.application'
apply from: '../findbugs.gradle'
apply from: '../pmd.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    buildTypes {
        all {
            proguardFiles(file('../proguard').listFiles())
            proguardFile getDefaultProguardFile('proguard-android.txt')
            minifyEnabled true
        }
        debug {
            debuggable true
        }
        release {
            debuggable false
        }
    }
    defaultConfig {
        applicationId "de.tum.in.tumcampus"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 514
        versionName "1.4.8-dev"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        checkAllWarnings true
        htmlReport false
        abortOnError false
        lintConfig new File("lint.xml")
        disable 'GoogleAppIndexingWarning',
                'TrulyRandom',
                'ContentDescription',
                'UnusedResources',    // unused resources get purged by shrinkResources
                'SelectableText'      // disabled by default
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    publishNonDefault true
}

def androidSupportVersion = '25.3.1'
def retrofitVersion = '2.2.0'
def espressoVersion = '2.2.2'

configurations.all {
    resolutionStrategy {
        force "com.android.support:design:$androidSupportVersion"
        force "com.android.support:recyclerview-v7:$androidSupportVersion"
        force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}


dependencies {
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:preference-v7:$androidSupportVersion"
    compile 'com.google.android.gms:play-services-gcm:10.2.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.guava:guava:20.0'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile 'com.github.chrisbanes:PhotoView:1.3.0'
    compile 'me.dm7.barcodescanner:zxing:1.9'
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    compile 'com.github.alamkanak:android-week-view:1.2.6'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'net.danlew:android.joda:2.9.5'
    compile 'com.github.franmontiel:PersistentCookieJar:v1.0.0'

    androidTestCompile "com.android.support:support-annotations:$androidSupportVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
}
