// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
        options.compilerArgs << "-Werror"
    }
    repositories {
        jcenter()
    }

    // Since not all modules have the same android plugin, we cannot apply the android plugin right
    // now. In afterEvaluate, we can add common android properties for all modules
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 24
                buildToolsVersion '24.0.0'
                defaultConfig {
                    minSdkVersion 10
                    targetSdkVersion 24
                    multiDexEnabled true
                    versionCode 67
                    versionName "1.4.5-dev"
                }
                lintOptions {
                    checkAllWarnings true
                    htmlReport false
                    warningsAsErrors true
                    abortOnError true
                    lintConfig new File("lint.xml")
                }
                buildTypes {
                    debug {
                        debuggable true
                    }
                    release {
                        debuggable false
                    }
                    all {
                        minifyEnabled true
                        FileCollection proGuardFileCollection = files {
                            file('proguard').listFiles()
                        }
                        proguardFiles(proGuardFileCollection)
                        proguardFile getDefaultProguardFile('proguard-android.txt')
                    }
                }
            }
        }
    }
}
